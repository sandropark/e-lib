rootProject.name = 'ecrick'

["ecrick-crawler-new"].each { moduleName ->
    def moduleDir = makeDir(rootDir, moduleName)

    ["crawler", "domain"].each {subName ->
        def subDir = makeDir(moduleDir, subName)
        makeGradle(subDir)
        makeSrc(subDir)

        def projectName = ":${moduleName}-${subDir.name}"
        include projectName
        project(projectName).projectDir = subDir
    }
    include ":${moduleName}"
}

["ecrick-repository", "ecrick-admin", "ecrick-app", "ecrick-crawler"].each {moduleName ->
    def moduleDir = makeDir(rootDir, moduleName)
    makeGradle(moduleDir)
    makeSrc(moduleDir)
    include ":${moduleName}"
}

private static File makeDir(File parent, String moduleName) {
    def moduleDir = new File(parent, moduleName)
    if (!moduleDir.exists()) moduleDir.mkdir()
    moduleDir
}

private static void makeGradle(File compDir) {
    def gradleFile = new File(compDir.absolutePath, "build.gradle")

    if (!gradleFile.exists()) {
        gradleFile.text =
                """
                dependencies {
              
            
                }
                """.stripIndent(16)
    }
}

private static void makeSrc(File compDir) {
    [
            "src/main/java/com/ecrick",
            "src/main/resources",
            "src/test/java/com/ecrick",
            "src/test/resources"
    ].each { srcDir ->
        def srcFolder = new File(compDir.absolutePath, srcDir)

        if (!srcFolder.exists())
            srcFolder.mkdirs()
    }
}
